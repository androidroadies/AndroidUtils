apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.kt3k.coveralls'

version ='1.0'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 23
    }

    buildTypes {
 	debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }
}

coveralls {
    sourceDirs = files('library/src/main/java').flatten()
    jacocoReportPath = "${buildDir}/reports/coverage/debug/report.xml"
}

tasks.coveralls {
    dependsOn 'connectedAndroidTest'
    onlyIf { System.env.'CI' }
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.2.0'
    compile files('libs/socialauth-4.4.jar')
    compile files('libs/linkedin-j-android-1.0.jar')
    compile files('libs/signpost-commonshttp4-1.2.1.1.jar')
    compile files('libs/signpost-core-1.2.1.1.jar')
    compile files('libs/scribe-1.3.0.jar')
    compile 'com.github.traex.rippleeffect:library:1.2.3'
    compile 'com.android.support:design:23.2.0'
    compile 'com.loopj.android:android-async-http:1.4.9'
}

//#################################### Bintray ####################################//
if (project.rootProject.file('local.properties').exists()) {

def siteUrl = 'https://github.com/multidots/android-app-common-tasks/'        // Homepage URL of the library
def gitUrl = 'https://github.com/multidots/android-app-common-tasks.git'      // Git repository URL

group = "com.multidots"

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'android-common-tasks'
                description = 'Android App Common Tasks library is developed to reduce efforts to achieve common tasks of the android apps quickly.'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'MIT'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id 'multidots'
                        name 'Multidots Solutions Pvt Ltd'
                        email 'info@multidots.in'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
//    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

// https://github.com/bintray/gradle-bintray-plugin
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "md-maven"
        // it is the name that appears in bintray when logged
        name = "android-common-tasks"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["MIT"]
        publish = true
        version {
            gpg {
                sign = false //Determines whether to GPG sign the files. The default is false
//                passphrase = properties.getProperty("bintray.gpg.password") //Optional. The passphrase for GPG signing'
            }
        }
    }
}
}